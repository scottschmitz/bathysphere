plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion buildConfig.compileSdk
    defaultConfig {
        applicationId "com.sschmitz.bathysphere"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode buildConfig.version.code
        versionName buildConfig.version.name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'

            resValue "string", "GOOGLE_SIGN_IN_CLIENT_ID", System.getenv("BATHYSPHERE_GOOGLE_SIGN_IN_CLIENT_ID")
            resValue "string", "GOOGLE_SIGN_IN_CLIENT_SECRET", System.getenv("BATHYSPHERE_GOOGLE_SIGN_IN_CLIENT_SECRET")
            resValue "string", "account_type", android.defaultConfig.applicationId
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        debug {
            def password = System.getenv("BATHYSPHERE_KEYSTORE_PASSWORD")
            storeFile file("$rootProject.projectDir/keystores/bathysphere.keystore")
            storePassword password
            keyAlias 'bathysphere'
            keyPassword password
        }

        release {
            def password = System.getenv("BATHYSPHERE_KEYSTORE_PASSWORD")
            storeFile file("$rootProject.projectDir/keystores/bathysphere.keystore")
            storePassword password
            keyAlias 'bathysphere'
            keyPassword password
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.compose
        kotlinCompilerVersion versions.kotlin
    }
}

dependencies {
    implementation project(':google-photos-api')

    implementation deps.kotlin.stdlib
    implementation deps.androidX.coreKtx
    implementation deps.androidX.appcompat
    implementation deps.androidX.lifecycleKtx
    implementation deps.google.material
    implementation deps.compose.ui
    implementation deps.compose.material
    implementation deps.compose.uiTooling

    implementation deps.accompanist.coil
}
